apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.nomanswallpaper"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 7
        versionName "1.1.0"
        resConfigs 'en', 'pt'
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            useProguard false
            buildConfigField 'Boolean', 'RELEASE', 'false'
            resValue 'string', 'app_name', 'NMW Dev'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
            resValue 'string', 'app_name', 'NMW'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    lintOptions {
        disable 'ParcelCreator'
    }
}

kapt {
    useBuildCache = true
}

androidExtensions {
    experimental = true
    defaultCacheImplementation = "SPARSE_ARRAY"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.27.0"
    implementation "com.github.adrielcafe.androidcoroutinescopes:appcompat:1.0.0"
    implementation "com.github.adrielcafe.androidcoroutinescopes:viewmodel:1.0.0"

    // Android
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXVersion"
    implementation "androidx.preference:preference:$androidXVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.fragment:fragment-ktx:$ktxVersion"
    implementation("android.arch.work:work-runtime-ktx:$workVersion"){
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-analytics:16.0.3"
    implementation "com.google.firebase:firebase-config:16.0.0"
    implementation "com.google.firebase:firebase-perf:16.0.0"
    implementation "com.google.firebase:firebase-messaging:17.3.2"
    implementation "com.google.firebase:firebase-firestore:17.1.0"
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.5"

    // View
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    implementation "com.mikepenz:fastadapter:$fastAdapterVersion"
    implementation "com.mikepenz:fastadapter-commons:$fastAdapterVersion"
    implementation "io.github.kobakei:ratethisapp:1.2.0"
    implementation "com.github.lawloretienne:discreteslider:0.0.9"
    implementation "com.hlab.fabrevealmenu:fab-reveal-menu:1.0.4"
    implementation "com.github.Kennyc1012:MultiStateView:1.3.2"
    implementation "com.github.adrielcafe:StickySwitch:356b9d8d26"

    // Util
    implementation "com.tencent:mmkv:1.0.10"
    implementation "org.koin:koin-androidx-viewmodel:1.0.1"
    implementation "org.greenrobot:eventbus:3.1.1"
    implementation "com.github.ajalt:timberkt:1.5.1"
    implementation "com.github.tinsukE:icekick:0.4"
    implementation "com.github.StephenVinouze:KinApp:1.0.2"
    implementation "com.markodevcic.peko:peko:0.32"
}

apply plugin: 'com.google.gms.google-services'